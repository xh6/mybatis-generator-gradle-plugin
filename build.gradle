buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}


apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

group 'com.xh6'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}


configurations {
    provided
    compile.extendsFrom provided
    deployerJars
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

ext {
    nexus_url = "maven.me:8081"
    nexus_username = "admin"
    nexus_password = "admin123"
}

repositories {
    maven { url "http://${nexus_url}/nexus/content/groups/public/" }
}


dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.mybatis.generator:mybatis-generator-core:1.3.7"
    compile "mysql:mysql-connector-java:5.1.38"
    compile "org.apache.commons:commons-lang3:3.7"
    compile "org.dom4j:dom4j:2.0.0"
    compile "com.alibaba:fastjson:1.2.44"

    testCompile "commons-io:commons-io:2.6"
    testCompile "junit:junit:4.12"
    testCompile "org.codehaus.groovy:groovy-all:2.4.6"
    testCompile "ch.qos.logback:logback-classic:1.2.3"
    testCompile "org.slf4j:log4j-over-slf4j:1.7.25"
    testCompile "org.slf4j:jcl-over-slf4j:1.7.25"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

def siteUrl = 'https://github.com/xh6/mybatis-generator-gradle-plugin'                        // #CONFIG# // project homepage
def gitUrl = 'https://github.com/xh6/mybatis-generator-gradle-plugin.git'                     // #CONFIG# // project git

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'mybatis-generator-gradle-plugin'
                description 'Oh hi, this is a mybatis generator gradle plugin'
                name 'mybatis-generator-gradle-plugin'  // 可默认，项目描述
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //  可默认，开发者信息
                        id 'zhouxinghai'
                        name 'zhouxinghai'
                        email '1093320666@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "mybatis-generator-gradle-plugin"
        userOrg = user
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

//1.0



